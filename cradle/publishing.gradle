apply plugin: 'maven'
apply plugin: 'signing'

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives.onlyIf { ['ossrhUsername', 'ossrhPassword'].every { project.hasProperty(it) } }
uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name "${title}"
                packaging 'jar'
                description "${description}"
                url 'http://induct.io'

                scm {
                    connection "scm:git:git://github.com/esuomi/${Project.name}.git"
                    developerConnection "scm:git:git@github.com:esuomi/${Project.name}.git"
                    url "https://github.com/esuomi/${Project.name}"
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        id 'esuomi'
                        name 'Esko Suomi'
                        email 'suomi.esko@gmail.com'
                    }
                }
            }
        }
    }
}