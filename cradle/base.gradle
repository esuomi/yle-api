apply plugin: 'maven'
apply plugin: 'signing'

repositories {
    mavenLocal()
}

dependencies {
    // slf4j
    compile group:'org.slf4j', name:'slf4j-api', version:'1.7.9'

    // better collections and other assorted utils
    compile group: 'com.google.guava', name:'guava', version:'18.0+'

    // these are the EXACT dependencies one has to use when using the JUnit+Mockito combo to avoid Hamcrest
    // compilation/runtime problems
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.+'
    testCompile group: 'org.hamcrest', name: 'hamcrest-junit', version: '2.0.0.0'
}

compileJava {
    options.encoding = "UTF-8"
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name "${title}"
                artifactId "${Project.name}"
                packaging 'jar'
                description "${description}"
                url 'http://induct.io'

                scm {
                    connection "scm:git:git://github.com/esuomi/${Project.name}.git"
                    developerConnection "scm:git:git@github.com:esuomi/${Project.name}.git"
                    url "https://github.com/esuomi/${Project.name}"
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        id 'esuomi'
                        name 'Esko Suomi'
                        email 'suomi.esko@gmail.com'
                    }
                }
            }
        }
    }
}